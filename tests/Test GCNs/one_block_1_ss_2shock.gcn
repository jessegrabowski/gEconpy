block STEADY_STATE
{
    definitions
    {
        L_num = theta * (1 - alpha) * (1 - beta * (1 - delta));
        L_denom = 1 - alpha * theta - beta * (1 - delta * (1 - alpha) - alpha * theta);
    };

    identities
    {
        L[ss] =  L_num / L_denom;
        K[ss] = (((1 - alpha * theta) * L[ss] - theta * (1 - alpha)) / (delta * (1 - theta) * L[ss])) ^
            (1 / (1 - alpha)) * L[ss];
        A[ss] = 1;
        B[ss] = 1;

        Y[ss] = K[ss] ^ alpha * L[ss] ^ (1 - alpha);
        I[ss] = delta * K[ss];
        C[ss] = Y[ss] - I[ss];

        lambda[ss] = theta * (C[ss] ^ theta * (1 - L[ss]) ^ (1 - theta)) ^ (1 - tau) / C[ss];
        q[ss] = lambda[ss];
        U[ss] = (C[ss] ^ theta * (1 - L[ss]) ^ (1 - theta)) ^ (1 - tau) / ((1 - beta) * (1 - tau));
    };
};

block HOUSEHOLD
{
	definitions
	{
		u[] = B[] * (C[] ^ theta * (1 - L[]) ^ (1 - theta)) ^ (1 - tau) / (1 - tau);
	};

	controls
	{
		C[], L[], I[], K[], Y[];
	};

	objective
	{
		U[] = u[] + beta * E[][U[1]];
	};

	constraints
	{
		Y[] = A[] * K[-1] ^ alpha * L[] ^ (1 - alpha);
		C[] + I[] = Y[] : lambda[];
		K[] = I[] + (1 - delta) * K[-1] : q[];
	};

	identities
	{
		log(A[]) = rho_A * log(A[-1]) + epsilon_A[];
		log(B[]) = rho_B * log(B[-1]) + epsilon_B[];
	};

	shocks
	{
		epsilon_A[], epsilon_B[];
	};

	calibration
	{
		alpha = 0.35;
		theta = 0.357;
		beta = 0.99;
		delta = 0.02;
		tau = 2;
		rho_A = 0.95;
		rho_B = 0.95;
	};
};

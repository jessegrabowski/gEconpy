options
{

};

tryreduce
{

};

block HOUSEHOLD
{
	definitions
	{
		u[] = (C[] ^ (1 - gamma) - 1) / (1 - gamma);
	};

	controls
	{
		C[], K[];
	};

	objective
	{
		U[] = u[] + beta * E[][U[1]];
	};

	constraints
	{
		C[] + K[] - (1 - delta) * K[-1] = A[] * K[-1] ^ alpha : lambda[];
	};

	identities
	{
		log(A[]) = rho * log(A[-1]) + epsilon[];
	};

	shocks
	{
		epsilon[] ~ N(mean=0, sd=sigma_epsilon);
	};

	calibration
	{
		alpha ~ Beta(mean=0.5, sd=0.1) = 0.4;
		beta  = 0.99;
		delta = 0.02;
		rho ~ Beta(mean=0.95, sd=0.04) = 0.95;
		gamma ~ HalfNormal(sigma=1) = 1.5;

		sigma_epsilon ~ Inv_Gamma(mean=0.1, sd=0.01) = 0.01;
	};


};
